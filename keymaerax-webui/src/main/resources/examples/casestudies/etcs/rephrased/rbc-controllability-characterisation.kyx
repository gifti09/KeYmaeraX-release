/**
 * Characterises the constraint ensuring that RBC preserves controllability
 * @provable
 * @see "Andre Platzer and Jan-David Quesel. European Train Control System: A case study in formal verification. In Karin Breitman and Ana Cavalcanti, editors, 11th International Conference on Formal Engineering Methods, ICFEM, Rio de Janeiro, Brasil, Proceedings, volume 5885 of LNCS, pages 246-265. Springer, 2009."
 *       Proposition 2
 */
Functions.
  R b.
End.

ProgramVariables.
  R vdes.
  R SB.
  R mo.
  R t.
  R a.
  R v.
  R z.
  R m.
  R d.
  R do.
  R em. /* emergency brake: 0=off, 1=on */
End.

Problem.
  em = 0
& d >= 0
& b > 0 ->
  [ do := d; mo := m;
		{
		   vdes :=*; ?vdes >= 0; do := d; mo := m; m :=*; d :=*;
		++ em := 1;
		}
	] ((do^2 - d^2 <= 2*b*(m-mo) & do >= 0 & d >= 0)
        <->
        \forall z \forall v ((v^2 - do^2 <= 2*b*(mo-z) & v >=0 & do >= 0 & b > 0)
                             -> (v^2 - d^2 <= 2*b*(m-z) & v >=0 & d >= 0 & b > 0))
    )
End.
