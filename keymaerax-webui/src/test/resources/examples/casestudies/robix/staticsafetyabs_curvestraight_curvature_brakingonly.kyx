Functions.
  R B.    /* minimum braking capability of the robot */
  R A.    /* maximum acceleration -B <= a <= A */
  R abs(R).  /* predefined function of absolute value */
End.

ProgramVariables.
  R x.    /* robot position: x */
  R y.    /* robot position: y */
  R v.    /* robot translational velocity */
  R a.    /* robot translational acceleration */
  R dx.   /* robot orientation: x */
  R dy.   /* robot orientation: y */
  R w.    /* robot rotational velocity */
  R k.    /* curvature of curve (i)straight: k=0; (ii)curve: k!=0 */
  R xo.   /* position of obstacle */
  R yo.
End.

Problem.
    v >= 0
  & ( abs(x-xo) > v^2 / (2*B)
    | abs(y-yo) > v^2 / (2*B) )
  & dx^2 + dy^2 = 1
  & A >= 0
  & B > 0
  ->
  [
    {
      /* brake on current curve */
      a := -B;

      /* dynamics */
      { x' = v * dx,
        y' = v * dy,
        dx' = -w * dy,
        dy' = w * dx,
        v' = a,
        w' = a*k
        & v >= 0
      }
    }*
  ] (x - xo)^2 + (y - yo)^2 > 0
End.